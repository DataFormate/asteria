document ::=
	statement-list-opt

identifier ::=
	PCRE([A-Za-z_][0-9A-Za-z_]*)
literal ::=
	null-literal | boolean-literal | string-literal | numeric-literal | nan-literal | infinity-literal
null-literal ::=
	"null"
boolean-litearl ::=
	"false" | "true"
string-literal ::=
	PCRE("([^\\]|(\\([abfnrtv'"\\]|(x[0-9A-Fa-f]{2})|(u[0-9A-Fa-f]{4})|(U[0-9A-Fa-f]{6}))))*?")
numeric-literal ::=
	binary-literal | octal-literal | decimal-literal | hexadecimal-literal
binary-literal ::=
	PCRE(0b[01]+(\.[01]+)([pP][-+]?[0-9]+)?)
octal-literal ::=
	PCRE(0[0-7]*(\.[0-7]+)([pP][-+]?[0-9]+)?)
decimal-literal ::=
	PCRE([1-9][0-9]*(\.[0-9]+)([eE][-+]?[0-9]+)?)
hexadecimal-literal ::=
	PCRE(0[xX][0-9A-Fa-f]+(\.[0-9A-Fa-f]+)([pP][-+]?[0-9]+)?)
nan-literal ::=
	"nan"
infinity-literal ::=
	"infinity"

statement ::=
	expression-statement | compound-statement |
	variable-definition | function-definition |
	if-statement | switch-statement |
	do-while-statement | while-statement | for-statement | foreach-statement |
	try-statement | defer-statement |
	label-statement | case-label-statement | default-label-statement |
	goto-statement | break-statement | continue-statement | throw-statement | return-statement

expression-statement ::=
	expression-opt ";"
compound-statement ::=
	"{" statement-list-opt "}"
statement-list-opt ::=
	statement-list | ""
statement-list ::=
	statement statement-list-opt

variable-definition ::=
	( "var" | "const" ) identifier equal-initailizer-opt ";"
equal-initializer-opt ::=
	"=" initializer | ""
initializer ::=
	"[" initializer-list-opt "]" | "{" key-value-list-opt "}" | expression
initializer-list-opt ::=
	initializer-list | ""
initializer-list ::=
	initializer ( "," initializer-list-opt | "" )
key-value-list-opt ::=
	key-value-list | ""
key-value-list ::=
	( string-literal | identifier ) "=" initializer ( "," key-value-list-opt | "" )
function-definition ::=
	"function" identifier "(" parameter-list-opt ")" compound-statement
parameter-list-opt ::=
	parameter-list | ""
parameter-list ::=
	identifier equal-initializer-opt ( "," parameter-list-opt | "" )

if-statement ::=
	"if" "(" expression ")" compound-statement ( "else" else-branch | "" )
else-branch ::=
	if-statement | compound-statement
switch-statement ::=
	"switch" "(" expression ")" compound-statement

do-while-statement ::=
	"do" compound-statement "while" "(" expression ")" ";"
while-statement ::=
	"while" "(" expression ")" compound-statement
for-statement ::=
	"for" "(" statement expression-opt ";" expression-opt ")" compound-statement
foreach-statement ::=
	"foreach" "(" "const" identifier "," ( "var" | "const" ) identifier "in" initializer ")" compound-statement

try-statement ::=
	"try" compound-statement "catch" "(" ( "var" | "const" ) identifier ")" compound-statement
defer-statement ::=
	"defer" compound-statement

label-statement ::=
	"label" identifier ":"
case-label-statement ::=
	"case" ( null-literal | boolean-literal | string-literal | numeric-literal ) ":"
default-label-statement ::=
	"default" ":"

goto-statement ::=
	"goto" identifier ";"
break-statement ::=
	"break" ";"
continue-statement ::=
	"continue" ";"
throw-statement ::=
	"throw" expression-opt ";"
return-statement ::=
	"return" expression-opt ";"

expression-opt ::=
	expression | ""
expression ::=
	prefix-operator-expression | expression-initiator expression-trailer-opt
prefix-operator-expression ::=
	( "+" | "-" | "~" | "!" | "++" | "--" ) expression

expression-initiator ::=
	identifier | literal | lambda-expression | nested-expression
lambda-expression ::=
	"lambda" "(" identifier-list-opt ")" compound-statement
nested-expression ::=
	"(" expression ")"

expression-trailer-opt ::=
	expression-trailer | ""
expression-trailer ::=
	postfix-operator-trailer | infix-operator-trailer | ternary-trailer | assignment-trailer |
	function-call-trailer | subscripting-trailer | member-access-trailer
postifix-operator-trailer ::=
	( "++" | "--" ) expression-trailer-opt
infix-operator-trailer ::=
	( "+" | "-" | "*" | "/"  | "%"  | "<<" | ">>" | ">>>" |
	  "&" | "|" | "^" | "&&" | "||" | "==" | "!=" | "<"   | ">" | "<=" | ">=" ) expression
ternary-trailer ::=
	"?" expression-opt ":" expression
assignment-trailer ::=
	( "+=" | "-=" | "*=" | "/="  | "%="  | "<<=" | ">>=" | ">>>=" |
	  "&=" | "|=" | "^=" | "&&=" | "||=" | "="   ) expression

function-call-trailer ::=
	"(" argument-list-opt ")" expression-trailer-opt
argument-list-opt ::=
	argument-list | ""
argument-list ::=
	expression-opt ( "," argument-list-opt | "" )
subscripting-trailer ::=
	"[" expression "]" expression-trailer-opt
member-access-trailer ::=
	"." identifier expression-trailer-opt
