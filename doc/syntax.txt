document ::=
	statement-list-opt

identifier ::=
	PCRE([A-Za-z_][0-9A-Za-z_]*)
literal ::=
	null-literal | boolean-literal | string-literal | numeric-literal
null-literal ::=
	"null"
boolean-litearl ::=
	"false" | "true"
numeric-literal ::=
	binary-literal | octal-literal | decimal-literal | hexadecimal-literal
binary-literal ::=
	PCRE(0b[01]+)
octal-literal ::=
	PCRE(0[0-7]*)
decimal-literal ::=
	PCRE([1-9][0-9]*([eE][+-]?[0-9]+)?)
hexadecimal-literal ::=
	PCRE(0[xX][0-9A-Fa-f]+([pP][0-9]+)?)
string-literal ::=
	PCRE("([^\\]|(\\([abfnrtv'"\\]|(x[0-9A-Fa-f]{2})|(u[0-9A-Fa-f]{4})|(U[0-9A-Fa-f]{6}))))*?")

identifier-list-opt ::=
	identifier-list | ""
identifier-list ::=
	identifier ( "," identifier-list-opt | "" )
expression-list-opt ::=
	expression-list | ""
expression-list ::=
	expression ( "," expression-list-opt | "" )
key-value-list-opt ::=
	key-value-list | ""
key-value-list ::=
	( string-literal | identifier ) "=" initializer ( "," key-value-list-opt | "" )

initializer ::=
	"[" expression-list-opt "]" | "{" key-value-list-opt "}" | expression

statement-list-opt ::=
	statement-list | ""
statement-list ::=
	statement statement-list-opt

statement ::=
	null-statement | compound-statement | expression-statement |
	variable-definition | constant-definition | using-declaration | function-definition |
	if-statement | else-statement | do-while-statement | while-statement | for-statement | foreach-statement | switch-statement |
	try-statement | defer-statement |
	label-statement | case-statement |
	goto-statement | break-statement | continue-statement | throw-statement | return-statement

null-statement ::=
	";"
compound-statement ::=
	"{" statement-list-opt "}"
expression-statement ::=
	expression ";"

variable-definition ::=
	"var" identifier ( "=" initializer | ";" )
constant-definition ::=
	"const" identifier ( "=" initializer | ";" )
using-declaration ::=
	"using" identifier "for" expression ";"
function-definition ::=
	"function" identifier "(" identifier-list-opt ")" compound-statement

if-statement ::=
	"if" "(" expression ")" compound-statement
else-statement ::=
	"else" ( if-statement | compound-statement )
do-while-statement ::=
	"do" compound-statement "while" "(" expression ")" ";"
while-statement ::=
	"while" "(" expression ")" compound-statement
for-statement ::=
	"for" "(" statement expression ";" expression ")" compound-statement
foreach-statement ::=
	"foreach" "(" identifier "," identifier "in" expression ")" compound-statement
switch-statement ::=
	"switch" "(" expression ")" compound-statement

try-statement ::=
	"try" compound-statement "catch" "(" identifier ")" compound-statement
defer-statement ::=
	"defer" compound-statement

label-statement ::=
	"label" identifier ":"
case-statement ::=
	"case" ( string-literal | numeric-literal ) ":"

goto-statement ::=
	"goto" identifier ";"
break-statement ::=
	"break" ";"
continue-statement ::=
	"continue" ";"
throw-statement ::=
	"throw" ( expression-statement | null-statement )
return-statement ::=
	"return" ( expression-statement | null-statement )

expression ::=
	subexpression | prefix-expression | lambda-expression | infix-or-postfix-expression
subexpression ::=
	"(" expression ")"
prefix-expression ::=
	( "+" | "-" | "~" | "!" | "++" | "--" ) expression
lambda-expression ::=
	"lambda" "(" identifier-list-opt ")" compound-statement expression
infix-or-postfix-expression ::=
	( literal | identifier ) (infix-trailer | postfix-trailer | ternary-trailer | "")
infix-trailer ::=
	( "+"  | "-"  | "*"  | "/"  | "%"  | "<<"  | ">>"  | ">>>"  | "&"  | "|"  | "^"  | "&&"  | "||"  |
	  "+=" | "-=" | "*=" | "/=" | "%=" | "<<=" | ">>=" | ">>>=" | "&=" | "|=" | "^=" | "&&=" | "||=" |
	  "==" | "!=" | "<" | ">" | "<=" | ">=" ) expression
postifix-trailer ::=
	"++" | "--" | "(" expression-list-opt ")" | "[" expression "]" | "." identifier
ternary-trailer ::=
	"?" expression ":" expression
